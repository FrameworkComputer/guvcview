dnl Process this file with autoconf to produce a configure script.

dnl Package definitions
AC_INIT(guvcview, 1.1.0,[<pj.assis@gmail.com>])
AM_INIT_AUTOMAKE(guvcview, 1.1.0)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

dnl Set gettext package name
GETTEXT_PACKAGE=guvcview
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [guvcview])

dnl Add the languages which your application supports here.
ALL_LINGUAS="pt pt_BR de es fr da tr cs ja"
AM_GLIB_GNU_GETTEXT([external])

dnl check for dependencies
PKG_CHECK_MODULES(PACKAGE, [gtk+-2.0 >= 2.10.0 glib-2.0 >= 2.10.0 gthread-2.0 gdk-2.0 >= 2.10.0 sdl >= 1.2.10 portaudio-2.0 libpng twolame libavcodec])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

dnl Check for pulseaudio
PKG_CHECK_MODULES(PULSE, [libpulse-simple],
  [AC_DEFINE(PULSEAUDIO,1,[Define if pulse headers presence detected])], 
  [ echo '--------------------------------------------------'
    echo 'Pulse Audio development package not detected'
    echo 'Compiling Guvcview without Pulse Support.'
    echo '--------------------------------------------------'])

AC_SUBST(PULSE_CFLAGS)
AC_SUBST(PULSE_LIBS)

dnl Check for avcodec.h directly
dnl if all tests fail must add avcodec.h path to C_INCLUDE_PATH before running configure
AC_CHECK_HEADER([avcodec.h], [has_avcodec_h="yes"])

dnl Check old install path for avcodec.h (ffmpeg/avcodec.h)
AC_CHECK_HEADER([ffmpeg/avcodec.h], [has_ffmpeg_dir="yes"])

dnl Check new install path for avcodec.h (libavcodec/avcodec.h)
dnl if all the checks fail we include "libavcodec/avcodec.h"
AC_CHECK_HEADER([libavcodec/avcodec.h], [has_libavcodec_dir="yes"])

if test "$has_avcodec_h" = yes; then
  AC_DEFINE(HAS_AVCODEC_H,1,[Define if avcodec.h can be acessed directly]) 
fi
if test "$has_ffmpeg_dir" = yes; then
  AC_DEFINE(HAS_FFMPEG_AVCODEC_H,1,[Define if avcodec.h installed to ffmpeg dir]) 
fi
if test "$has_libavcodec_dir" = yes; then
  AC_DEFINE(HAS_LIBAVCODEC_AVCODEC_H,1,[Define if avcodec.h installed to libavcodec dir])
fi

dnl Check if the processor stores words with the most significant byte first 
dnl (like Motorola and SPARC, unlike Intel and VAX).
AC_C_BIGENDIAN 

dnl Write output
AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
pixmaps/Makefile
])
